def xtextVersion = '2.25.0'

dependencies {
    implementation "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
    implementation "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
    // https://mvnrepository.com/artifact/org.eclipse.platform/org.eclipse.core.runtime
    implementation group: 'org.eclipse.platform', name: 'org.eclipse.core.runtime', version: '3.22.0'
    // https://mvnrepository.com/artifact/org.eclipse.platform/org.eclipse.core.resources
    implementation group: 'org.eclipse.platform', name: 'org.eclipse.core.resources', version: '3.15.0'
    // https://mvnrepository.com/artifact/org.eclipse.emf/org.eclipse.emf.mwe2.launch
    implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.mwe2.launch', version: '2.12.1'
}
configurations {
    mwe2 {
        extendsFrom implementation
    }
}

dependencies {
    mwe2 'org.eclipse.emf:org.eclipse.emf.mwe2.launch'
    mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}"
    mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"
}

task generateXtextLanguage(type: JavaExec) {
    main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = configurations.mwe2
    inputs.file "org.lflang/src/org/lflang/GenerateLinguaFranca.mwe2"
    inputs.file "org.lflang/src/org/lflang/LinguaFranca.xtext"
    outputs.dir "src-gen/main/lflang"
    args += "org.lflang/src/org/lflang/GenerateLinguaFranca.mwe2"
    args += "-p"
    args += "rootPath=/${projectDir}"
}

generateXtext.dependsOn(generateXtextLanguage)
compileJava.dependsOn(generateXtext)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]

sourceSets {
    main {
        java {
            srcDirs += ['org.lflang/src-gen', 'org.lflang/src']
        }
        resources {
            srcDirs += ['org.lflang/src-gen', 'org.lflang/src']
        }
    }
}


//    classpath += file("org.lflang/src-gen/org/lflang/LF.xtextbin")
