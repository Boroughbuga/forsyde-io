/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4.1/userguide/tutorial_java_projects.html
 */
// Apply the java plugin to add support for Java
plugins {
  id 'java'
  id 'idea'
  id 'eclipse'
  id 'maven-publish'
  //id "io.freefair.lombok" version "5.3.0"
  id "com.jfrog.bintray" version "1.8.5"
  id 'maven'
  id 'signing'
}

ext {
  artifactName = 'forsyde-io-java'
  organization = "forsyde"
}

// due to how maven central works, the groupId must be the same as the
// website which we host
group = 'io.github.forsyde'
archivesBaseName = ext.artifactName
version = '0.2.0'


// In this section you declare where to find the dependencies of your project
repositories {
  // Use 'jcenter' for resolving your dependencies.
  // You can declare any Maven/Ivy/file repository here.
  mavenCentral()
  jcenter()
  mavenLocal()
}

// In this section you declare the dependencies for your production and test code
dependencies {
  // The production code uses the SLF4J logging API at compile time
  implementation 'org.slf4j:slf4j-api:1.7.25'
  implementation group: 'org.jgrapht', name: 'jgrapht-core', version: '1.5.0'

  testImplementation 'junit:junit:4.12'
}

sourceCompatibility = 1.8


bintray {   
    user = bintrayUser
    key = bintrayKey
    pkg {
        repo = "${group}"
        name = "${artifactName}"
        userOrg = "${organization}"
        licenses = ['MIT', 'BSD 3-Clause']
        vcsUrl = 'https://github.com/forsyde/forsyde-io'
        version {
          name = "${version}"
          released = new Date()
        }
    }
    publications = ['maven']
}

java { 
    withSourcesJar()
    withJavadocJar()
}

jar {
	archiveFileName = "${artifactName}" + "-" + "${version}" + ".jar"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'ForSyDe IO Java'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'ForSyDe IO Java Supporting libraries.'
        url 'https://github.com/forsyde/forsyde-io'

        scm {
          connection 'scm:git:https://github.com/forsyde/forsyde-io.git'
          developerConnection 'scm:git:git@github.com:forsyde/forsyde-io.git'
          url 'https://github.com/forsyde/forsyde-io'
        }

        licenses {
          license {
            name 'The MIT License'
            url 'https://choosealicense.com/licenses/mit/'
          }
        }

        developers {
          developer {
            id 'Jordao'
            name 'Rodolfo Jordao'
            email 'jordao@kth.se'
          }
        }
      }
    }
  }
}

test {
    useJUnit()
    maxHeapSize = '1G'
}

eclipse {
  project {
    name = artifactName
  }
}



artifacts {
    archives javadocJar, sourcesJar
}

signing {
	useGpgCmd()
    sign configurations.archives
}
