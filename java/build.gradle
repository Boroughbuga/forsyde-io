buildscript {

}

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'java'
  id 'idea'
  id 'eclipse'
  //id "io.freefair.lombok" version "5.3.0"
  //id "com.jfrog.bintray" version "1.8.5"
  id 'maven'
  id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

apply from: 'generator.gradle'

ext {
  artifactName = 'forsyde-io-java'
}

// due to how maven central works, the groupId must be the same as the
// website which we host
group = 'io.github.forsyde'
archivesBaseName = ext.artifactName
version = '0.2.2'

// In this section you declare where to find the dependencies of your project
repositories {
  // Use 'jcenter' for resolving your dependencies.
  // You can declare any Maven/Ivy/file repository here.
  mavenCentral()
  jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
  // The production code uses the SLF4J logging API at compile time
  implementation 'org.slf4j:slf4j-api:1.7.25'
  implementation group: 'org.jgrapht', name: 'jgrapht-core', version: '1.5.0'

  testImplementation 'junit:junit:4.12'
}

sourceCompatibility = 1.11

sourceSets {
  main {
    java {
      srcDirs += ['src-gen/main/java']
    }
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

jar {
    archiveFileName = "${artifactName}" + '-' + "${version}" + '.jar'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = artifactName
      from components.java
      pom {
        name = 'ForSyDe IO Java'
        description = 'ForSYDe IO Java (+JVM) supporting library'
        url = 'https://github.com/forsyde/forsyde-io'
        packaging = 'jar'
        scm {
          connection = 'scm:git:https://github.com/forsyde/forsyde-io.git'
          developerConnection = 'scm:git:git@github.com:forsyde/forsyde-io.git'
          url = 'https://github.com/forsyde/forsyde-io'
        }
        licenses {
          license {
            name = 'The MIT License'
            url = 'https://choosealicense.com/licenses/mit/'
          }
        }
        developers {
          developer {
            id = 'Jordao'
            name = 'Rodolfo Jordao'
            email = 'jordao@kth.se'
          }
        }
      }
    }
  }
  repositories {
    maven {
      name = 'OSSRH'
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('MAVEN_USERNAME') ?: System.getenv('OSSRH_USERNAME') ?: ''
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('MAVEN_PASSWORD') ?: System.getenv('OSSRH_PASSWORD') ?: ''
      }
    }
  // maven {
  //   name = "GitHubPackages"
  //   url = uri("https://maven.pkg.github.com/forsyde/forsyde-io")
  //   credentials {
  //     username = System.getenv("GITHUB_ACTOR") ?: ""
  //     password = System.getenv("GITHUB_TOKEN") ?: ""
  //   }
  // }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      username = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv('MAVEN_USERNAME') ?: System.getenv('OSSRH_USERNAME') ?: ''
      password = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv('MAVEN_PASSWORD') ?: System.getenv('OSSRH_PASSWORD') ?: ''
    }
  }
}

test {
    useJUnit()
    maxHeapSize = '1G'
}

eclipse {
  project {
    name = artifactName
  }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
  // small hack to work offline or online
  if (project.hasProperty('signing.keyId')) {
    useGpgCmd()
  } else {
    useInMemoryPgpKeys(
      System.getenv('GPG_SIGNING_KEY') ?: findProperty('signing.key'),
      System.getenv('GPG_SIGNING_PASSWORD') ?: findProperty('signing.password') ?: '' //default to an empty password
    )
  }
  sign publishing.publications.mavenJava
  sign configurations.archives
}
